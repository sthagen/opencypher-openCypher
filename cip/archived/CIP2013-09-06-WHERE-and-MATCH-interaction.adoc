:numbered:
:toc:
:toc-placement: macro

= CIP20130906A - WHERE and MATCH interaction

*Author:* Chris Leishman <chris.leishman@neotechnology.com>

toc::[]

== Background
Cypher has two keywords, `MATCH` and `WHERE`, that together control how parts of the graph are selected for using in following clauses (eg. `WITH`, `RETURN`). There are two commonly used, yet differing, ways to model the interaction of these two clauses and it is necessary for us to identify which is the most suitable for the language.

== Model of Interaction 1: WHERE filters matches

In this model, the `MATCH` clause identifies parts of the graph based solely on the expressed pattern. For example, “++MATCH (a)-->(b)++” would match every pair of connected nodes in the graph.

As a filter, the expression provided in a `WHERE` clause will be evaluated in the context of each match found, and a positive (true) result from that evaluation would allow the selected part of the graph to be used in the following clauses of the query. For example, if “++WHERE a.name = "Bob"++” had been specified, then this expression would be run for every pair of connected nodes in the graph (substituting the first matched node for ‘a’ in the expression), resulting in true only where the first node has a name property of ‘Bob’, and thus only providing those matches to later clauses of the query.

For example, given the following graph:

    CREATE (Joe {name: "Joe"})
    CREATE (Bob {name: "Bob"})
    CREATE (Sue {name: "Sue"})
    CREATE (Joe)-[:KNOWS]->(Bob)
    CREATE (Joe)-[:KNOWS]->(Sue)
    CREATE (Bob)-[:KNOWS]->(Joe)
    CREATE (Bob)-[:KNOWS]->(Sue)

Evaluation occurs as follows:

[options="header"]
|===========================================================================
| a                      | b                      | matched? | selected?
| `Node[3]{name: "Joe"}` | `Node[6]{name: "Bob"}` | Yes      | No
| `Node[3]{name: "Joe"}` | `Node[2]{name: "Sue"}` | Yes      | No
| `Node[5]{name: "Bob"}` | `Node[9]{name: "Joe"}` | Yes      | Yes
| `Node[5]{name: "Bob"}` | `Node[9]{name: "Sue"}` | Yes      | Yes
| `Node[7]{name: "Sue"}` | <not found>            | No       | <not matched>
|===========================================================================
[small]#(Note that the last row is not identified as a match - it is included only to show that Node 7 (Sue) was considered during matching.)#

An important observation is that the rest of the query never sees the matches that were not selected. Thus it is possible that an implementation may not strictly follow this process and may instead optimise the evaluation to skip matching parts of the graph that would never be selected. _Such optimisation can only occur such that the result stays consistent with this model._

== Model of Interaction 2: WHERE informs matching

In this model, the `MATCH` clause uses both the expressed pattern and the `WHERE` expression to identify matches in the graph, which are then selected for use in the following clauses of the query with no further evaluation. This is done by considering the `WHERE` expression at every step of the matching process.

For example, given the same data as previously, “++MATCH (a)-->(b) WHERE a.name = "Bob"++” would only match connected pairs of nodes where the first of that pair has a name property of ‘Bob’. ie:

[options="header"]
|========================================================================
| a                      | b                      | matched and selected?
| `Node[3]{name: "Joe"}` | <not attempted>        | No
| `Node[5]{name: "Bob"}` | `Node[3]{name: "Joe"}` | Yes
| `Node[5]{name: "Bob"}` | `Node[9]{name: "Sue"}` | Yes
| `Node[7]{name: "Sue"}` | <not attempted>        | No
|========================================================================
[small]#(Note that the first and last row in the table above are never identified as matches, and thus this example yields the same selected results in either model.)#

As matching is a progressive process (a traversal of the graph), it is necessary that this model evaluates the `WHERE` expression at every step of the matching process, and it is _axiomatic that this expression not evaluate to false at any stage of the process_. This has the effect of implicitly eliminating branches of possibility.

For example: when node 7 (Joe) is matched to the first node of the pattern, the use of identifier ‘++a++’ will evaluate to node 7 (Joe) when checking the expression (we use the terminology _“bound”_ to describe this temporary connection of an identifier to a node or relationship). Thus the expression “++a.name = "Bob"++” will evaluate to false at this stage, and there is thus no need to attempt further matching (as can be seen in the example above).

An important observation in this model, is that a `WHERE` expression may make use of any identifier from the `MATCH` clause (eg. “++WHERE a.name = "Bob" AND b.name = "Sue"++”) and given the stated axiom, it is interesting to consider how the progressive matching process evaluates the `WHERE` expression when identifiers have not yet been “bound”.

=== Un-bound identifiers whilst evaluating expressions during matching

Using the previous example and an expression of “++a.name = "Bob" AND b.name = "Sue"++”, consider the matching process starting when node 7 (Joe) is bound to identifier ‘++a++’. By substituting the known values, we can see the expression evaluates as follows:

    a.name = "Bob" AND b.name = "Sue"
    ⇒ "Joe" = "Bob" AND <unknown> = "Sue"
    ⇒ false AND <unknown>
    ⇒ false

Note that the identifier ‘++b++’ is unknown at this point of the matching process. Given that, it is impossible to determine the right hand side of the `AND` expression, yet it is still provable that the overall result will be false. And thus matching can stop at this point.

[small]#(Note that the use of <unknown> in these example evaluations does not represent a value that could be observed in the use of Cypher. It is used in this context merely to represent the logical concept of an unknown state, for the purposes of explanation. Whilst an implementation may choose to use such a ternary logic model in runtime evaluation, it is not a requirement to do so. Any other implementation that satisfies the same logical outcome is sufficient.)#

Consider next the matching starting when node 5 (Bob) is bound to ‘++a++’. Again substituting known values:

    a.name = "Bob" AND b.name = "Sue"
    ⇒ "Bob" = "Bob" AND <unknown> = "Sue"
    ⇒ true AND <unknown>
    ⇒ <unknown>

In this case, the left hand side of the `AND` expression is clearly true, yet the right hand side remains unknown. Given this, it is unknown if the expression would be true or false when ‘++b++’ is bound, and thus matching continues: first binding ‘++b++’ to node 3 (Joe), which yields a false from evaluation of the `WHERE` expression, and then to node 9 (Sue) which yields a true. ie.

[options="header"]
|========================================================================
| a                      | b                      | matched and selected?
| `Node[3]{name: "Joe"}` | <not attempted>        | No
| `Node[5]{name: "Bob"}` | `Node[3]{name: "Joe"}` | No
| `Node[5]{name: "Bob"}` | `Node[9]{name: "Sue"}` | Yes
| `Node[7]{name: "Sue"}` | <not attempted>        | No
|========================================================================

Thus the outcome is as would be expected, and remains consistent to either model.

=== Type checking and un-bound identifiers

Cypher expressions can be statically type-checked, by evaluating only what the possible types of an expression (or identifier) could be, and whether the expression is semantically valid given those. Thus type errors can be detected regardless of whether the expression is used in a context where some identifiers may be un-bound during evaluation.

== Comparison of models

Given that the only functionally visible result from either approach is the selected matches that are made available to later clauses of the query, both models produce ostensibly identical outcomes. Thus they have both been interchangeably used to describe the behaviour of the Cypher language. However, there are is a functionally visible aspect in which they differ: when the `MATCH` specifies a pattern containing an optional portion. In this scenario, the two models have quite different results.

=== Understanding Optional Matching

It is helpful to understand the intended behaviour of an optional portion of a pattern. Optional pattern portions ensure that a match is still made available to following clauses in the query even if the nothing can possibly satisfy the optional portion. Specifically, if nothing is found to satisfy the optional portion of the match, then a single match will be provided to later clauses in the query and the use of any expression relying on identifiers declared in the optional portion of the pattern will result in a null.

For example, given the same data as previously, “++MATCH (a)-[?]->(b) WHERE a.name = "Bob" OR a.name = "Sue" RETURN a, b++” will yield the following results:

|================================================
| a _(non-optional)_     | b _(optional)_
| `Node[5]{name: "Bob"}` | `Node[3]{name: "Joe"}`
| `Node[5]{name: "Bob"}` | `Node[9]{name: "Sue"}`
| `Node[9]{name: "Sue"}` | `null`
|================================================

Note that this result has selected one additional match over the previous examples: a match on node 9 (Sue) with the optional portion of the pattern set to null. This is a valid result for two reasons:

1. the remainder of the pattern, after the a node, is optional; and
2. there are no parts of the graph rooted at node 9 that could have satisfied the pattern

This set of results is consistent for either model under discussion.

=== Optional Matching with Model 1: WHERE filters matches

Given the previous example query, “++MATCH (a)-[?]->(b?) WHERE a.name = "Bob" OR a.name = "Sue" RETURN a, b++”, we can look at how this is evaluated in the model of `WHERE` being a filter.

[small]#(Note that examples will be using the proposed syntax for indicating optional nodes. Whilst this is not currently valid syntax, it is helpful to avoid having to use an explicit “++START a=node(*)++” statement in every example)#

In this example, the `MATCH` clause would select every pair of connected nodes in the graph. Additionally, _it would select every node in the graph where no match for the optional relationship (and destination node) can be found_.

As a filter, the expression provided in the `WHERE` clause would then be evaluated in the context of each match found, and a positive (true) result from that evaluation would select that part of the graph to be used in the following clauses of the query. In this example, evaluation occurs as follows:

[options="header"]
|====
| a (non-optional)       | b (optional)           | matched? | selected?
| `Node[3]{name: "Joe"}` | `Node[6]{name: "Bob"}` | Yes      | No
| `Node[3]{name: "Joe"}` | `Node[2]{name: "Sue"}` | Yes      | No
| `Node[5]{name: "Bob"}` | `Node[3]{name: "Joe"}` | Yes      | Yes
| `Node[5]{name: "Bob"}` | `Node[9]{name: "Sue"}` | Yes      | Yes
| `Node[7]{name: "Sue"}` | <nothing matched>      | Yes      | Yes
|====

This is fairly straightforward, and the result is consistent with the intention of optional matching and either model of interaction.

==== Use of an optional identifier in a filtering WHERE expression

A more interesting and germane example occurs when the `WHERE` expression involves identifiers declared in the optional part of the pattern, for example “++MATCH (a)-[?]->(b?) WHERE (a.name = "Bob" OR a.name = "Sue") AND b.name = "Sue"++”. In this case, the `WHERE` expression places a constraint on the identifier ‘++b++’, which is optional in the pattern.

In this model, the filtering behaviour of `WHERE` requires evaluation of the expression for all the matches (as before). For the last match, this presents an interesting situation: nothing was matched for the optional part of the pattern. How to evaluate this?

In the previous section, “Understanding Optional Matching”, the following behaviour was identified: _“use of any expression relying on identifiers declared in the optional portion of the pattern will result in a null”_. In this model of interaction, this statement applies not only to expressions in a `RETURN` clauses but also to the expressions in the `WHERE` clause.

Substituting the values, we can see the expression evaluates as follows:

    (a.name = "Bob" OR a.name = "Sue") AND b.name = "Sue"
    ⇒ ("Sue" = "Bob" OR "Sue" = "Sue") AND null = "Sue"
    ⇒ true AND false
    ⇒ false

Given this, the results from this example would be as follows:

[options="header"]
|=======================================================================
| a _(non-optional)_     | b _(optional)_         | matched? | selected?
| `Node[3]{name: "Joe"}` | `Node[6]{name: "Bob"}` | Yes      | No
| `Node[3]{name: "Joe"}` | `Node[2]{name: "Sue"}` | Yes      | No
| `Node[5]{name: "Bob"}` | `Node[3]{name: "Joe"}` | Yes      | No
| `Node[5]{name: "Bob"}` | `Node[9]{name: "Sue"}` | Yes      | Yes
| `Node[7]{name: "Sue"}` | <nothing matched>      | Yes      | No
|=======================================================================

Interestingly, this is exactly the same result as would occur if there was no optionality in the pattern. The optionality did allow the `MATCH` clause to select node 7 (Sue), but the use of the unmatched optional portion in the `WHERE` clause then caused it to be discarded again.

==== IS NULL check of an optional identifier in filtering WHERE expression

Another example would be the use of an explicit NULL check in the where expression, for example “++MATCH (a)-[?]->(b?) WHERE (a.name = "Bob" OR a.name = "Sue") AND b IS NULL++”. In this case, the `WHERE` clause (in the context of the 4th match) evaluates as follows:

    (a.name = "Bob" OR a.name = "Sue") AND b IS NULL
    ⇒ ("Sue" = "Bob" OR "Sue" = "Sue") AND null IS NULL
    ⇒ true AND true
    ⇒ true

Thus the results from this example are as follows:

[options="header"]
|=======================================================================
| a _(non-optional)_     | b _(optional)_         | matched? | selected?
| `Node[3]{name: "Joe"}` | `Node[6]{name: "Bob"}` | Yes      | No
| `Node[3]{name: "Joe"}` | `Node[2]{name: "Sue"}` | Yes      | No
| `Node[5]{name: "Bob"}` | `Node[3]{name: "Joe"}` | Yes      | No
| `Node[5]{name: "Bob"}` | `Node[9]{name: "Sue"}` | Yes      | No
| `Node[7]{name: "Sue"}` | <nothing matched>      | Yes      | Yes
|=======================================================================
[small]#Note that the 3rd match was not selected in this example, as the value bound to the ‘++b++’ identifier, node 9 (Sue), was certainly not null.#


=== Optional Matching with Model 2: WHERE informs matching

Again using the example query “++MATCH (a)-[?]->(b?) WHERE a.name = "Bob" OR a.name = "Sue" RETURN a, b++”, we can consider how this is evaluated in the model of `WHERE` informing the matching process.

In this model, the `MATCH` clause uses both the pattern and the `WHERE` expression to identify matches in the graph. First a match for the non-optional part of the pattern is found in the graph, and this match is then extended to find complete matches over the entire pattern. However, in the case that no matches can be found over the entire pattern, then the non-optional part is still selected and made available to later clauses in the query.

For example:

[options="header"]
|========================================================================
| a _(non-optional)_     | b _(optional)_        | matched and selected?
| `Node[3]{name: "Joe"}` |                        | <nothing matched>
| `Node[5]{name: "Bob"}` | `Node[3]{name: "Joe"}` | Yes
| `Node[5]{name: "Bob"}` | `Node[9]{name: "Sue"}` | Yes
| `Node[7]{name: "Sue"}` | <nothing matched>      | Yes
|========================================================================

This is fairly straightforward, and the result is consistent with the intention of optional matching and either model of interaction.

==== Use of an unbound identifier in a WHERE expression informing matching

Let’s now again consider the more complex example “++MATCH (a)-[?]->(b?) WHERE (a.name = "Bob" OR a.name = "Sue") AND b.name = "Sue"++”, and how it evaluates in the combined `MATCH` and `WHERE` model.

As stated, this model supposes that a match is first found for the non-optional portion of the pattern (using the `WHERE` expression to inform what is valid to match). If this is found, then it is extended to match the optional portion. Should no matches for the optional portion be found, then the non-optional portion only is selected. Thus it follows that the results from this example are:

[options="header"]
|====
| a _(non-optional)_     | b _(optional)_         | matched and selected?
| `Node[3]{name: "Joe"}` |                        | <nothing matched>
| `Node[5]{name: "Bob"}` | `Node[9]{name: "Sue"}` | Yes
| `Node[7]{name: "Sue"}` | <nothing matched>      | Yes
|====

Note the last result: a match for node 7 (Sue) was found for the non-optional part, but a match for the longer pattern, including the non-optional part, could be. Thus only the non-optional part was selected. Importantly, this differs from the previous model where this match was not selected.

The evaluation of the `WHERE` expression in this case is identical to that considered in the discussion of “Un-bound identifiers whilst evaluation expressions during matching” above. The `WHERE` expression is evaluated at each stage of the matching process. For the case where only node 7 (Sue) has been considered, we can substitute values and consider the evaluation of the expression:

    (a.name = "Bob" OR a.name = "Sue") AND b.name = "Sue"
    ⇒ ("Sue" = "Bob" OR "Sue" = "Sue") AND <unknown> = "Sue"
    ⇒ true AND <unknown>
    ⇒ <unknown>

Considering the axiom stated previously, that _the expression not evaluate to false at any stage of the process_, it is apparent that we can continue at this point. Interestingly, it is also a logical axiom that _any expression where the result is unknown will evaluate true should the clauses involving the unknown identifier be removed_. This logical axiom allows us to accept this as a valid match for the non-optional portion of the pattern and thus, when no matches can be found for the whole pattern, it is valid to select only the non-optional part.

==== IS NULL check of an optional identifier in WHERE expression informing matching

The other example, “++MATCH (a)-[?]->(b?) WHERE (a.name = "Bob" OR a.name = "Sue") AND b IS NULL++”, shows an interesting effect of the evaluation process used in this model.

Considering the match starting with node 5 (Bob), we can look at how the evaluation occurs at the first step by again substituting values:

    (a.name = "Bob" OR a.name = "Sue") AND b IS NULL
    ⇒ ("Bob" = "Bob" OR "Bob" = "Sue") AND <unknown> IS NULL
    ⇒ true AND <unknown>
    ⇒ <unknown>

As discussed, the two axioms presented allow this to be considered as a valid match for the non-optional portion of the pattern.

Next the match is extended to include node 3 (Joe), which is “bound” to the identifier ‘++b++’. Again substituting values:

    (a.name = "Bob" OR a.name = "Sue") AND b IS NULL
    ⇒ ("Bob" = "Bob" OR "Bob" = "Sue") AND Node[3] IS NULL
    ⇒ true AND false
    ⇒ false

The definitively false result from this evaluation means that this match cannot be considered. Likewise, node 9 (Sue) is tried:

    (a.name = "Bob" OR a.name = "Sue") AND b IS NULL
    ⇒ ("Bob" = "Bob" OR "Bob" = "Sue") AND Node[9] IS NULL
    ⇒ true AND false
    ⇒ false

Again, this match cannot be considered.

Given that these represent the only nodes connected to node 5 (Bob), it is clear that nothing matches the entire pattern. Thus the only the non-optional part is matched.

Summarising, this example results in the following:

[options="header"]
|===================================================================
| a _(non-optional)_     | b _(optional)_    | matched and selected?
| `Node[3]{name: "Joe"}` |                   | <nothing matched>
| `Node[5]{name: "Bob"}` | <nothing matched> | Yes
| `Node[7]{name: "Sue"}` | <nothing matched> | Yes
|===================================================================

Importantly, this result again differs from the previous model. In this example an additional match was selected, one where the identifier ‘++a++’ was bound to node 5 (Bob) and identifier ‘++b++’ was left unbound (and would thus evaluate to null).

=== Search space differential between models

Whilst performance is largely an implementation concern, it is worth briefly considering the different algorithmic models from the perspective of their search space.

Using the example of “++MATCH (a)-->(b) WHERE a.name = "Bob" AND b.name = "Sue" RETURN a, b++”, and following the models as described, we can observe a substantial increase in graph exploration when the `WHERE` expression is used to filter after the matching.

For the filtering model, the following matches are found and then evaluated:

[options="header"]
|===========================================================================
| a                      | b                      | matched? | selected?
| `Node[3]{name: "Joe"}` | `Node[6]{name: "Bob"}` | Yes      | No
| `Node[3]{name: "Joe"}` | `Node[2]{name: "Sue"}` | Yes      | No
| `Node[5]{name: "Bob"}` | `Node[9]{name: "Joe"}` | Yes      | No
| `Node[5]{name: "Bob"}` | `Node[9]{name: "Sue"}` | Yes      | Yes
| `Node[7]{name: "Sue"}` | <not found>            | No       | <not matched>
|===========================================================================

This approach visits 7 nodes and 4 relationships, to find 4 complete matches that are then considered for selection. All but one are eliminated at this point. Contrast to the use of the `WHERE` expression during matching:

[options="header"]
|========================================================================
| a                      | b                      | matched and selected?
| `Node[3]{name: "Joe"}` | <not attempted>        | No
| `Node[5]{name: "Bob"}` | `Node[3]{name: "Joe"}` | No
| `Node[5]{name: "Bob"}` | `Node[9]{name: "Sue"}` | Yes
| `Node[7]{name: "Sue"}` | <not attempted>        | No
|========================================================================

In this approach, 5 nodes and 2 relationships are visited, yielding the same single match as a result. Obviously this is a significant difference, even in this very simple example.

Interestingly, and as mentioned previously, it is possible for an implementation to optimise the filtering example to reduce the search space and thus approach the performance complexity of the latter model. This can be achieved by moving parts of the `WHERE` expression into the matching process in the same way that the latter model does implicitly. However, _any implementation must only do so where it can be proven that the outcome will remain equivalent_. Given that the output of the first model is not the same as the second, specifically where there are optional parts in the pattern, some expressions must either be excluded from this optimisation approach or a logical interpretation of these expressions must be made in order to determine which parts are safe to use during matching, and which are not.

=== Implementation complexity differential between models

Both implementations are relatively straightforward for any language implementation, however the filtering model does not require any form of evaluation that can consider unknown states, which theoretically makes it a simpler approach (note that “simple” is used not in the manner of easy, but in the manner of “not complex”).

However, as discussed in the previous section, a performant implementation of the filtering approach would necessarily need to include a process of optimisation that moves at least part of the expression evaluation into the progressive matching. Thus it will also need to deal with unknowns in expressions at that stage, in addition to having a separate optimisation process that is otherwise inherent in the combined `MATCH` and `WHERE` model. Given this, it is reasonable to conclude that the combined `MATCH` and `WHERE` model (model 2) would be a substantially simpler approach for a working implementation.

== Converting between models

In the context of each of the two proposed models, it is interesting to consider how to construct a query that achieves the same outcome as the other.

=== Informing matching whilst using Model 1

Considering the previous examples, it has been shown that the model using `WHERE` to inform the matching process (model 2) is more accepting - it allows matches that the other model, `WHERE` filtering matches, does not. Whilst this discussion paper has not proven that no converse example exists, it is the opinion of the author that it does not and that _the combined `MATCH` and `WHERE` model always produces a superset of selections when compared to the `WHERE` filtering model_.

Given this, it is not obviously possible to achieve the same output as Model 2 when treating `WHERE` as a filter.

=== Filtering matches whilst using Model 2

As discussed, model 2 (`WHERE` informing matching) produces a superset of the result of the filtering model (model 1). Thus a query interpreted according to model 2 can be trivially made to provide the same result as model 1, by adding an explicit filtering step. For example, take the query “++MATCH (a)-[?]->(b?) WHERE (a.name = "Bob" OR a.name = "Sue") AND b.name = "Sue"++”. Interpreted according to model 1 gives the following:

[options="headers"]
|================================================
| a _(non-optional)_     | b _(optional)_
| `Node[5]{name: "Bob"}` | `Node[9]{name: "Sue"}`
|================================================

The same query, when interpreted according to model 2 gives:

[options="headers"]
|========================================================
| a _(non-optional)_     | b _(optional)_
| `Node[5]{name: "Bob"}` | `Node[9]{name: "Sue"}`
| `Node[9]{name: "Sue"}` | <nothing matched> (ie. `null`)
|========================================================

To achieve the same results as model 1, the query could be rewritten such that the part of the `WHERE` expression that uses the optional identifiers is instead made an explicit filter after a `WITH` clause, ie: “++MATCH (a)-[?]->(b) WHERE (a.name = "Bob" OR a.name = "Sue") WITH a, b WHERE b.name = "Sue"++”.

[small]#Note: As discussed previously, the use of an optional identifier in a `WHERE` filter for any expression other than an `IS NULL` check, renders the optionality irrelevant. Thus an equally valid alternative for this example is to use the expression “++MATCH (a)-->(b) WHERE (a.name = "Bob" OR a.name = "Sue") AND b.name = "Sue"++”. This will give the same result in both models of evaluation.#

Considering an example that won’t match anything after starting with node 5 (‘Bob’): “++MATCH (a)-[?]->(b?) WHERE (a.name = "Bob" OR a.name = "Sue") AND b.name = "Lou"++”. This gives the following when evaluated under model 1:

[options="headers"]
|========================================================
| a _(non-optional)_     | b _(optional)_
| `Node[7]{name: "Sue"}` | <nothing matched> (ie. `null`)
|========================================================

For model 2:

[options="headers"]
|========================================================
| a _(non-optional)_     | b _(optional)_
| `Node[5]{name: "Bob"}` | <nothing matched> (ie. `null`)
| `Node[7]{name: "Sue"}` | <nothing matched> (ie. `null`)
|========================================================

Again, in the context of model 2, the part of the `WHERE` expression using the optional can be moved to an explicit filter, thus achieving the same results as model 1. The query then becomes “++MATCH (a)-[?]->(b?) WHERE (a.name = "Bob" OR a.name = "Sue") WITH a, b WHERE b.name = "Lou"++”.

== Decision

Cypher uses model 2.